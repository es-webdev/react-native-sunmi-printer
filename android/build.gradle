buildscript {
  if (project == rootProject) {
    repositories {
      google()
      mavenCentral()
    }

    dependencies {
      classpath 'com.android.tools.build:gradle:7.4.2'
    }
  }
}

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def isNewArchitectureEnabled() {
  return project.hasProperty('newArchEnabled') && project.newArchEnabled == 'true'
}

apply plugin: 'com.android.library'

if (isNewArchitectureEnabled()) {
  apply plugin: 'com.facebook.react'
}

android {
  compileSdkVersion safeExtGet('compileSdkVersion', 34)
  namespace 'com.reactnativesunmiprinter'
  defaultConfig {
    minSdkVersion safeExtGet('minSdkVersion', 21)
    targetSdkVersion safeExtGet('targetSdkVersion', 34)
    versionCode 1
    versionName "1.0"

  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }
  lintOptions {
    disable 'GradleCompatible'
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
  }

  sourceSets {
    main {
      if (!isNewArchitectureEnabled()) {
        java.srcDir('src/oldarch')
      } else {
        java.srcDir("${project.buildDir}/generated/source/codegen/java")
      }
    }
  }
}

repositories {
  mavenLocal()
  maven {
    // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
    url("$rootDir/../node_modules/react-native/android")
  }
  google()
  mavenCentral()
}

dependencies {
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-native:+"  // From node_modules
  implementation "com.sunmi:printerlibrary:1.0.23"
}

if (isNewArchitectureEnabled()) {
  react {
    jsRootDir = file("../src")
    libraryName = "RNReactNativeSunmiPrinterSpec"
    codegenJavaPackageName = "com.reactnativesunmiprinter"
  }
}
